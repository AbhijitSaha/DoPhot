	FUNCTION ISEARCH(ONESTAR, BIG, NOISE, NFAST, NSLOW)
	PARAMETER (MMAX = 11)
	INCLUDE 'TUNEABLE'
	PARAMETER (MAXFIL = 1024)
	COMMON /STARLIST/ STARPAR(NPMAX,NSMAX),IMTYPE(NSMAX),
     1	  SHADOW(NPMAX,NSMAX), SHADERR(NPMAX,NSMAX)
	COMMON /SEARCH/ NSTOT,THRESH
	COMMON /SUBRASTER/  Z(MAXFIL), XX(MAXFIL), YE(MAXFIL)
	COMMON /CRUDESTAT/ NPT, SUM0, SUM1, SUM2, MAXVAL, XM, YM
	COMMON /FITARRAYS/ A(NPMAX),FA(NPMAX),C(2*NPMAX+1,2*NPMAX+1),
     1    B(2*NPMAX), FB(2*NPMAX)
	INTEGER*2 BIG(NFAST,NSLOW)
	INTEGER*4 NOISE(NFAST,NSLOW)
	INTEGER*2 JRECT(2)
	EXTERNAL ONESTAR
	LOGICAL COSMIC, SKIP, OFFPIC, OBLIT, TOOFAINT, FLAG, WIPE,hole
	LOGICAL TOOBRIGHT, NUFFPTS, TRANSMASK
	DIMENSION ERR(NPMAX), dummy(npmax)
	DATA MAGIC / 2147483647/
	DATA IADD, ISUB / 1, -1 /
C:
C:  FIRST WE GO THROUGH STARSUBTRACTED DATA LOOKING FOR NEW STARS
C:
        pixt2 = pixthresh*pixthresh
	DO 2757 I = 1, 2				
	  JRECT(I) = IRECT(I)
	  IF (THRESH .GT. ITOP/2) THEN		
	    IRECT(I) = KRECT(I)			
	  END IF
2757	CONTINUE
	NSPREV = NSTOT
	CALL MAKEMASK(ONESTAR)
c	NTHPIX = SQRT(FLOAT(NFAST))
	DO 2758 I = 1, NSLOW
	  IY = I
	  IXMID = NFAST/2
	  BESTSKY = GUESS1(A,DUMMY,IXMID,IY)	
	  TTHRESH = BESTSKY + THRESH
	  THRESH2 = THRESH**2
	  DO 2759 J = 1, NFAST
	    IF (MOD(J,NTHPIX) .EQ. 1) THEN
              if(J .le. NFAST-NTHPIX/2) then
	        BESTSKY = PARINTERP(FLOAT(J+NTHPIX/2),FLOAT(I),DUMMY)
              else 
                BESTSKY = PARINTERP(FLOAT(J), FLOAT(I), DUMMY)
              end if
                TTHRESH = BESTSKY + THRESH
	    END IF
	    IF      (BIG(J,I) .LT. TTHRESH) THEN
	    ELSE IF (NOISE(J ,I) .GE. MAGIC) THEN
	    ELSE IF ((FLOAT(NOISE(J,I))) .GT. (THRESH2/pixt2)) THEN
	    ELSE
	    HIGHSKY = PARINTERP(FLOAT(J),FLOAT(I),DUMMY)	
	    HIGHTHRESH = HIGHSKY + THRESH
	    IF      (BIG(J,I) .LT. HIGHTHRESH) THEN
	    ELSE IF (TRANSMASK(BIG,NOISE,NFAST,NSLOW,J,I,HIGHSKY)) THEN
	  if(lverb.gt.20) then
	    write(6,*) ' Triggering on new Object:', J, I, BIG(J, I)
	  end if
	      CALL FILLERUP (BIG, NOISE, J, I, NFAST, NSLOW)
	      NUFFPTS = (NPT .GE. ENUFF4*IRECT(1)*IRECT(2))
	      IF (.NOT. NUFFPTS) THEN
	  if(lverb.gt.20) write(6,*) 'SKIPPING: NPT = ', NPT
	        SKIP = .TRUE.
	      ELSEIF(.NOT.TRANSMASK(BIG,NOISE,NFAST,NSLOW,J,I,SUM2))THEN
	  if(lverb.gt.20) write(6,*) 'FAILED TRANSMASK ON AVERAGE SKY'
	      ELSE
		SKY = GUESS1(A, DUMMY, J, I)	
C:
C:	WILL 2*NIT HELP ON OBLITERATIONS?
C:
		CHI=CHISQ(ONESTAR,XX,Z,YE,NPT,A,FA,C,NFIT1,ACC,ALIM,
     *                2*NIT)
		SKIP = OFFPIC(A,J,I,NFAST,NSLOW,DX,DY)
		IF (.NOT. SKIP) THEN
		  NSTOT = NSTOT + 1
	if(lverb.gt.20) write(6,*) 'THIS IS STAR NO. ', NSTOT
		  CALL PARUPD (A, STARPAR(1,NSTOT), J, I)
		  CALL ERRUPD (C, ERR, NFIT1)
		  WIPE = COSMIC(ONESTAR,BIG,NOISE,NFAST,NSLOW,
     1	    STARPAR(1,NSTOT))
		  WIPE = WIPE .OR. TOOBRIGHT(BIG,NOISE,NFAST,NSLOW,
     1	    STARPAR(1,NSTOT))
		  FLAG = TOOFAINT(STARPAR(1,NSTOT), ERR)
		  IF (FLAG) THEN
		    IMTYPE(NSTOT) = 7	
		  END IF
		  IF (WIPE) THEN
		    IMTYPE(NSTOT) = 8
          	    HOLE = OBLIT(ONESTAR,BIG,NOISE,NFAST,
     1			nslow,starpar(1,nstot))
		  ELSE IF (CHI .GE. 1E10) THEN
		    SKIP = .TRUE.
      if(lverb.gt.20) write(6,*) 
     *     'FAILED TO CONVERGE:  NO ENTRY IN STARLIST'
	            NSTOT = NSTOT - 1
		  ELSE
		    CALL ADDSTAR(ONESTAR,BIG,NOISE,NFAST,NSLOW,
     1	      STARPAR(1,NSTOT),ISUB)
		  END IF
		END IF
	      END IF
	    END IF
	    END IF
2759	  CONTINUE
2758	CONTINUE
	ISEARCH = NSTOT - NSPREV
	DO 2760 I = 1, 2
	  IRECT(I) = JRECT(I)			
2760	CONTINUE
	RETURN
	END
